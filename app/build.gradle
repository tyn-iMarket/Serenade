apply plugin: 'com.android.application'
apply plugin: 'deploygate'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.tynmarket.serenade"
        minSdkVersion 23
        targetSdkVersion 26
        // Auto increment https://qiita.com/mickamy/items/0285ce1035ec1a224ae7
        versionCode 2
        versionName "0.0.2-alpha"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        def properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        buildConfigField "String", "API_KEY", "\"${properties.getProperty("apiKey")}\""
        buildConfigField "String", "API_SECRET", "\"${properties.getProperty("apiSecret")}\""
        buildConfigField "String", "OGPSERVE_URL", "\"${properties.getProperty("ogpserveUrl")}\""
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Support Library
    implementation "com.android.support:appcompat-v7:${support_lib_version}"
    implementation "com.android.support:design:${support_lib_version}"
    implementation "com.android.support:recyclerview-v7:${support_lib_version}"
    implementation "com.android.support:cardview-v7:${support_lib_version}"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'

    // Firebase
    implementation "com.google.firebase:firebase-core:${firebase_lib_version}"
    implementation('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
        transitive = true
    }
    implementation "com.google.firebase:firebase-config:${firebase_lib_version}"

    // UI
    implementation 'de.hdodenhof:circleimageview:2.2.0'

    // Net
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.google.code.gson:gson:2.8.2'

    // Twitter
    implementation 'com.twitter.sdk.android:twitter-core:3.2.0'

    // Rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.9'

    // Utility
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'com.github.bumptech.glide:glide:4.5.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.5.0'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'

    // Test
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // TODO: Performance, Debug, Logging
}

apply plugin: 'com.google.gms.google-services'