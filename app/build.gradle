apply plugin: 'com.android.application'
apply plugin: 'deploygate'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.tynmarket.serenade"
        minSdkVersion 23
        targetSdkVersion 26
        // Auto increment https://qiita.com/mickamy/items/0285ce1035ec1a224ae7
        versionCode 2
        versionName "0.0.2-alpha"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        def properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        buildConfigField "String", "API_KEY", "\"${properties.getProperty("apiKey")}\""
        buildConfigField "String", "API_SECRET", "\"${properties.getProperty("apiSecret")}\""
        buildConfigField "String", "OGPSERVE_URL", "\"${properties.getProperty("ogpserveUrl")}\""
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }
    flavorDimensions "type"
    productFlavors {
        dummy {
            dimension "type"
        }
        normal {
            dimension "type"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:design:27.0.2'
    implementation 'com.android.support:recyclerview-v7:27.0.2'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.squareup.retrofit2:converter-gson:2.1.0'
    implementation 'com.twitter.sdk.android:twitter-core:3.1.1'
    implementation 'com.github.bumptech.glide:glide:4.5.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.5.0'
    implementation ("com.github.bumptech.glide:recyclerview-integration:4.5.0") {
        // Excludes the support library because it's already included by Glide.
        transitive = false
    }
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'com.squareup.retrofit2:retrofit:2.1.0'
    testImplementation 'junit:junit:4.12'
    implementation 'com.google.firebase:firebase-core:11.8.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
        transitive = true
    }
}

apply plugin: 'com.google.gms.google-services'